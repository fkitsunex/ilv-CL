#include "/utils/depth.glsl"



float getAoInfluence(float centerDepth, vec2 offset) {
	
	float depth1 = toBlockDepth(texture2D(DEPTH_BUFFER_WO_TRANS, texcoord + offset).r);
	float depth2 = toBlockDepth(texture2D(DEPTH_BUFFER_WO_TRANS, texcoord - offset).r);
	
	float diff1 = centerDepth - depth1;
	float diff2 = centerDepth - depth2;
	
	float diffTotal = diff1 + diff2;
	return 1.0 - 2.0 * abs(clamp(diffTotal, 0.001, 1.0) - 0.5);
	
}



float getAoAmount(float depth) {
	
	float blockDepth = toBlockDepth(depth);
	vec3 noise3 = texelFetch(noisetex, (texelcoord + frameCounter * 17) & 127, 0).rgb;
	float fovScale = gbufferProjection[1][1];
	// max(blockDepth, 6.0): upstream I-Like-Vanilla v1.4.4 perf fix. Without it, very small blockDepth
	// (standing right against a surface) blows up the sample radius, scattering the AO taps across a huge
	// screen area and thrashing the texture cache — a big FPS drop when close to objects.
	float scale = AO_SIZE * 0.25 / pow(max(blockDepth, 6.0), 1.3) * fovScale;
	vec2 offsetOffset = noise3.xy * scale * 0.125;
	vec2 offsetMult = vec2(scale * invAspectRatio, scale);
	
	float total = 0.0;
	const int SAMPLE_COUNT = AO_QUALITY * AO_QUALITY;
	for (int i = 1; i <= SAMPLE_COUNT; i++) {
		
		float len = float(i) / SAMPLE_COUNT;
		float angle = (len + noise3.b) * PI * 2.0;
		vec2 offset = vec2(cos(angle), sin(angle)) * len;
		offset *= offsetMult;
		offset += offsetOffset;
		
		total += getAoInfluence(blockDepth, offset);
		
	}
	total /= SAMPLE_COUNT;
	total *= 1.0 - clamp(blockDepth * invFar, 0.0, 1.0);
	
	return total;
}
